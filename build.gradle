plugins {
    id 'java'
    id 'maven-publish'
    id("io.github.goooler.shadow") version "8.1.7"
}

group = 'com.chunkslab.realms'
version = '1.0.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'

    group rootProject.group
    version rootProject.version

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
    }

    build {
        dependsOn shadowJar
    }
}

subprojects {
    apply plugin: 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.rapture.pw/repository/maven-snapshots/' }
    maven { url 'https://repo.triumphteam.dev/snapshots/' }
    maven { url "https://storehouse.okaeri.eu/repository/maven-public/" }
    maven { url 'https://repo.xenondevs.xyz/releases' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://github.com/deanveloper/SkullCreator/raw/mvn-repo/' }
}

dependencies {
    implementation project(':api')

    implementation("net.kyori:adventure-api:4.17.0")
    implementation("net.kyori:adventure-text-minimessage:4.17.0")
    implementation("net.kyori:adventure-text-serializer-legacy:4.17.0")
    implementation("net.kyori:adventure-platform-bukkit:4.3.3")

    implementation 'dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT'  exclude group: 'com.google.code.gson'
    implementation 'eu.okaeri:okaeri-configs-yaml-bukkit:5.0.5'
    implementation 'xyz.xenondevs.invui:invui:1.41'
    //implementation 'fr.mrmicky:fastboard:2.1.2'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'dev.dbassett:skullcreator:3.0.1'

    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.13'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependsOn(jar)

    archiveFileName = "Realms v${archiveVersion.get()}.jar"

    relocate 'net.kyori', 'com.chunkslab.realms.libs.adventure'
    relocate 'dev.triumphteam.cmd', 'com.chunkslab.realms.libs.triumphteam'
    relocate 'xyz.xenondevs.invui', 'com.chunkslab.realms.libs.invui'
    relocate 'dev.dbassett', 'com.chunkslab.realms.libs.skullcreator'
    relocate 'eu.okaeri', 'com.chunkslab.realms.libs.okaeri'
    relocate 'org.bstats', 'com.chunkslab.realms.libs.bstats'

    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

shadowJar.shouldRunAfter build

tasks.shadowJar {
    doLast {
        file("${buildDir}/libs/${project.name}-${version}.jar").delete()
    }
}

project(":api") {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                artifact shadowJar
            }
        }
        repositories {
            maven {
                url = uri("https://repo.chunkslab.com/release")
                credentials {
                    username = System.getenv("CHUNKSLAB_REPO_USERNAME")
                    password = System.getenv("CHUNKSLAB_REPO_PASSWORD")
                }
            }
        }
    }
}